other := p |-> ([o : o in players() | o != p])[1];
value := cachedProcedure(State, player, limit, alpha := -1, beta := 1) {
    if (finished(State)) {
        return utility(State, player);
    }
    if (limit == 0) {
        return 0;
    }
    val := -1;
    for (ns in nextStates(State, player)) {
        val := max({ val, -value(ns, other(player), limit - 1, -beta, -alpha) });
        if (val >= beta) {
            return val;
        }
        alpha := max({ val, alpha });
    }
    return val;
};
best_move := procedure(State, player, limit := 5) {
    bestVal := value(State, player, limit);
    NS      := nextStates(State, player);
    BestNS  := {ns : ns in NS | -value(ns,other(player),limit-1) == bestVal};
    return [bestVal, rnd(BestNS)];
};
play_game := procedure() {
    State := startState();
    print(stateToString(State));
    while (!finished(State)) {
        start        := now();
        firstPlayer  := players()[1];
        [val, State] := best_move(State, firstPlayer);
        stop         := now();
        print(cacheStats(value));
        clearCache(value);
        print("For me, the game has the value $val$.  My move:");
        print(stateToString(State));
        print("computation took $(stop - start) * 0.001$ seconds");
        if (final_msg(State)) { return; }
        State := getMove(State);
        print(stateToString(State));
        if (final_msg(State)) { return; }
    }
};
////////////////////////////////////////////////////////////////////////////////
//                                                                            //
// Game specific code is below.                                               //
//                                                                            //
////////////////////////////////////////////////////////////////////////////////

players := [] |-> [ "X", "O" ];
startState := procedure() {
    Line   := [ " " : col in [1 .. 7] ];
    return [ Line : row in [1 .. 6] ];
};
nextStates := procedure(State, player) {
    states := {};
    for (col in [1..7]) {
        row := find_empty(State, col);
        if (row != 7) {
            states += { updatedState(State, player, row, col) };
        }
    }
    return states;
};
updatedState := procedure(State, player, row, col) {
    State[row][col] := player;
    return State;
};
find_empty := procedure(State, col) {
    for (row in [1..6] | State[row][col] == " ") {
        return row;
    }
    return 7;
};
utility := procedure(State, player) {
    for (line in all_lines()) {
        if (forall([row, col] in line | State[row][col] == player)) {
            return 1;
        }
        if (forall([row, col] in line | State[row][col] notin { player, " " })) {
            return -1;
        }
    }
    if (last_line_filled(State)) {
        return 0;
    }
};
all_lines := cachedProcedure() {
    vertical := { { [row, col] : row in [i..i+3] } : i in [1..3], col in [1..7] };
    horizontal := { { [row, col] : col in [i..i+3] } : i in [1..4], row in [1..6] };
    diagonal_up := { { [i+a, i+b] : i in [1..4] } : a in [0..2], b in [0..3] | 4+a <= 6 && 4+b <= 7 };
    diagonal_down := { { [(7-i)-a, i+b] : i in [1..4] } : a in [0..2], b in [0..3] | (7-4)-a >= 1 && 4+b <= 7};
    return vertical + horizontal + diagonal_up + diagonal_down;
};
last_line_filled := procedure(State) { 
    return " " notin State[6];
};
finished := State |-> utility(State, "X") != om;

////////////////////////////////////////////////////////////////////////////////

// for pretty printing
stateToString := procedure(State) {
    ls     := "\n" + "-" * (4 * 7 + 1) + "\n";  // line separator
    Board  := ls;
    for (row in [6, 5 .. 1]) {
        Line  := [State[row][col]: col in [1 .. 7]];
        Board += "| " + join(Line, " | ") + " |";
        Board += ls;
    }
    return Board;
};
getMove := procedure(State) {
    col := read("Your turn, enter move as a single number specifiying the column: ");
    col := int(col);
    row := find_empty(State, col);
    assert(row <= 6, "illegal move, game aborted");
    State[row][col] := "O";
    return State;
};
final_msg := procedure(State) {
    if (finished(State)) {
        msg := "The game is finished. ";
        if (utility(State, "O") == 1) {
            print(msg + "You have won!");
        } else if (utility(State, "O") == -1) {
            print(msg + "You lost!");
        } else {
            print(msg + "It's a draw.");
        }
        return true;
    }
    return false;
};

play_game();
