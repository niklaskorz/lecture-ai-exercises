load("sudoku-data.stlx");
load("csp-constraint-propagation.stlx");

// Given a sudoku puzzle, this function creates a CSP.
sudokuCSP := procedure(Puzzle) {
    // 3 groups vertically, 3 groups horizontally
    // 3 cells per group vertically, 3 cells per group horizontally
    L := { 1 .. 9 };

    // Vyx
    Variables := { "V$y$$x$" : x in L, y in L };
    
    Values := { 1 .. 9 };

    Constraints := {};
    // Provided puzzle
    Constraints += { "V$y$$x$ == $Puzzle[y][x]$" : x in L, y in L | Puzzle[y][x] != "*" };
    // All in one row different
    Constraints += +/ { allDifferent({ "V$y$$x$" : x in L }) : y in L };
    // All in one column different
    Constraints += +/ { allDifferent({ "V$y$$x$" : y in L }) : x in L };
    // All in one group different
    Constraints += +/ { allDifferent({ "V$y1 + 3 * y2$$x1 + 3 * x2$" : x1 in { 1 .. 3 }, y1 in { 1 .. 3 } }) : x2 in { 0 .. 2 }, y2 in { 0 .. 2 } };

    return [Variables, Values, Constraints];
};
// L is a list of numbers and variables.  For every x, y in L such that x is different
// from y and either x or y or both are variables, an inequation of the form x != y is
// generated.  The set of all of these inequations is returned.
allDifferent := procedure(L) {
    return { "$x$ != $y$" : x in L, y in L | x != y };
};
// Given a variable assignment and the original puzzle, this will print the solution
printSolution := procedure(Assignment, Puzzle) {
    length := 9 * 4 + 5;
    print("=" * length);
    for (row in [1..9]) {
        s := "|| ";
        for (col in [1..9]) {
            entry := Puzzle[row][col];
            if (col % 3 == 0) {
                sep := ' || ';
            } else {
                sep := ' | ';
            }
            if (isNumber(entry)) {
                s += entry + sep;
            } else if (Assignment["V$row$$col$"] != om) {
                s += Assignment["V$row$$col$"] + sep;
            } else {
                s += "*" + sep;
            }
        }
        print(s);
        if (row % 3 == 0) {
            print("=" * length);
        } else {
            print("-" * length);
        }
    }
};
main := procedure() {
    resetRandom();
    Puzzle := sudoku();
    print("Trying to solve the following puzzle:");
    printSolution({}, Puzzle);
    startTotal := now();
    CSP        := sudokuCSP(Puzzle);
    Solution   := solve(CSP);
    stopTotal  := now();
    all := (stopTotal - startTotal) / 1000.0;
    print("The total computation took $all$ seconds.");
    print("There were $#CSP[1]$ variables and $#CSP[3]$ constraints.");
    printSolution(Solution, Puzzle);
};
main();
